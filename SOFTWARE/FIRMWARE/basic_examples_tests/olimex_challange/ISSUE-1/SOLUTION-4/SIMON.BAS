   10 REM SIMON
   20 CLS
   30 REM CONSTANTS
   40 RED = 1 : GREEN = 2 : YELLOW = 3 : BLUE = 4
   50 CHANNEL =2 : AMPLITUDE = -15 : DURATION = 10
   60 LIGHTDURATION=50
   70 MAXSEQUENCELENGTH = 32
   80 REM INITIALISE SCREEN
   90 PROCBOX(300,300,200,YELLOW)
  100 PROCBOX(600,300,200,BLUE)
  110 PROCBOX(300,600,200,GREEN)
  120 PROCBOX(600,600,200,RED)
  130 REM GENERATE RANDOM SEQUENCES
  140 DIM SEQUENCE(32)
  150 FOR I=1 TO 8 SEQUENCE(I)=YELLOW NEXT I
  160 FOR I=9 TO 16 SEQUENCE(I)=BLUE NEXT I
  170 FOR I=17 TO 24 SEQUENCE(I)=GREEN NEXT I
  180 FOR I=25 TO 32 SEQUENCE(I)=RED NEXT I
  190 FOR N = MAXSEQUENCELENGTH TO 2 STEP -1
  200   RANDOMBOX = RND(N) : TMP = SEQUENCE(RANDOMBOX) : SEQUENCE(RANDOMBOX) = SEQUENCE(N) : SEQUENCE(N) = TMP
  210 NEXT N
  220 REM PLAY SEQUENCE
  230 REM FOR I = RED TO BLUE PROCFLASHBOX(I) : NEXT I
  240 REM GAME PLAY
  250 PRINT "GREEN: Press "; GREEN; ", RED: "; RED; ", YELLOW: "; YELLOW; ", BLUE: "; BLUE
  260 I = 0
  270 FOR J = J TO MAXSEQUENCELENGTH
  280   I = I+1
  290   PROCWAIT(LIGHTDURATION*2)
  300   FOR K = 1 TO I
  310     PROCFLASHBOX(SEQUENCE(K))
  320     PROCWAIT(LIGHTDURATION*2)
  330   NEXT K
  340   REM PROCESS INPUT
  350   L=0
  360   REPEAT
  370     L=L+1
  380     Key%=INKEY(200)
  390     IF Key% >=49 AND Key% <=52 THEN 
  400     RESULT = FNPROCESSKEYPRESS(VAL(CHR$(Key%)),SEQUENCE(L))
  410     IF NOT RESULT THEN Key% = -1
  420     ELSE Key% = -1
  430   UNTIL Key%= -1 OR L=I
  440   IF Key%=-1 THEN GOTO 490
  450 NEXT J
  470 PRINT "YOU WIN!"
  480 END
  490 PRINT "YOU LOSE!"
  500 END
  510 REM ****************
  520 DEF PROCBOX(STARTX,STARTY,SIDELENGTH,BOXCOLOUR)
  530 GCOL 0,BOXCOLOUR
  540 MOVE STARTX,STARTY
  550 DRAW STARTX+SIDELENGTH,STARTY
  560 DRAW STARTX+SIDELENGTH,STARTY+SIDELENGTH
  570 DRAW STARTX,STARTY+SIDELENGTH
  580 DRAW STARTX,STARTY
  590 PLOT 85,STARTX+SIDELENGTH,STARTY+SIDELENGTH
  600 PLOT 85,STARTX+SIDELENGTH,STARTY
  610 ENDPROC
  620 REM ****************
  630 DEF PROCFLASHRED
  640 COLOUR RED,255,0,0
  650 SOUND CHANNEL,AMPLITUDE,89,DURATION
  660 PROCWAIT(LIGHTDURATION)
  670 COLOUR RED,155,0,0
  680 ENDPROC
  690 REM ****************
  700 DEF PROCFLASHYELLOW
  710 COLOUR YELLOW,255,255,0
  720 SOUND CHANNEL,AMPLITUDE,57,DURATION
  730 PROCWAIT(LIGHTDURATION)
  740 COLOUR YELLOW,155,155,0
  750 ENDPROC
  760 REM ****************
  770 DEF PROCFLASHGREEN
  780 COLOUR GREEN,0,255,0
  790 SOUND CHANNEL,AMPLITUDE,21,DURATION
  800 PROCWAIT(LIGHTDURATION)
  810 COLOUR GREEN,0,155,0
  820 ENDPROC
  830 REM ****************
  840 DEF PROCFLASHBLUE
  850 COLOUR BLUE,0,0,255
  860 SOUND CHANNEL,AMPLITUDE,69,DURATION
  870 PROCWAIT(LIGHTDURATION)
  880 COLOUR BLUE,0,0,155
  890 ENDPROC
  900 REM ****************
  910 DEF PROCWAIT(DURATION)
  920 T = TIME : REPEAT UNTIL TIME-T>DURATION
  930 ENDPROC
  940 REM ****************
  950 DEF PROCFLASHBOX(BOXCOLOUR)
  960 IF BOXCOLOUR = RED THEN PROCFLASHRED
  970 IF BOXCOLOUR = BLUE THEN PROCFLASHBLUE
  980 IF BOXCOLOUR = YELLOW THEN PROCFLASHYELLOW
  990 IF BOXCOLOUR = GREEN THEN PROCFLASHGREEN
 1000 ENDPROC
 1010 REM ****************
 1020 DEF FNPROCESSKEYPRESS(KEY,CORRECT)
 1030 PROCFLASHBOX(KEY)
 1040 =(KEY=CORRECT)
