   10 REM @@@@@@@@@@@@@@@@@@
   20 REM @@@@@@ SIMON @@@@@
   30 REM @ by D.Whitworth @
   40 REM @ 23rd JULY 1983 @
   50 REM @@@@@@@@@@@@@@@@@@
   51 :
   52 REM "Resurrected" by Luzrbum (www.luzrbum.com) for:
   53 REM - the Olimex Coding Challenge
   54 REM - the YouTube video [TBA]
   55 REM 23rd April 2023
   60 REM *FX200,3
   70 high%=0
   80 DIM A%(100)
   90 DIM B%(4)
  100 FOR L=1 TO 4
  110   B%(L)=L*20
  120 NEXT L
  130 ON ERROR GOTO 9000
  140 REM *FX4,1
  150 REM *TV55
  160 MODE1
  170 PROCCOFF
  175 REM *FX200,2
  180 PROCINSTR
  190 MODE1
  200 PROCCOFF
  205 PROCRESETCOLS
  210 PROCDRAWSCRN
  220 REPEAT
  230   PROCDLY(30)
  240   PROCGAME
  250 UNTIL again$="n" OR again$="N"
  260 CALL!-4
 1000 DEFPROCINSTR
 1010 REM F$=CHR$141+CHR$131+CHR$157+CHR$132
 1020 TT$="SIMON "
 1030 PRINTTAB(13,0);TT$;TAB(13,1);TT$
 1040 PRINTTAB(5,4);"Simon is a game of memory."''"The computer repeats a sequence by"''"playing notes and flashing boxes on the"''"screen and you must try to copy this by"''"pressing the keys 1, 2, 3, 4 in the correct"''"order."
 1050 PRINT'"Each time you get it right the computer"''"will add one more note and repeat the"''"sequence.    GOOD LUCK"
 1060 PRINT''TAB(6)"PRESS THE SPACE BAR TO START"
 1065 REM *FX15,0
 1070 REPEATUNTILGET=32
 1071 PROCSOUNDS(1)
 1072 PROCSOUNDS(2)
 1073 PROCSOUNDS(3)
 1074 PROCSOUNDS(4)
 1075 PROCDLY(10)
 1076 PROCRESETCOLS
 1080 ENDPROC
 1090 :
 1100 :
 2000 DEFPROCDRAWSCRN
 2010 CLS
 2020 PROCSQUARE(7,50,50,850)
 2030 PROCSQUARE(4,100,100,350)
 2040 PROCSQUARE(3,500,100,350)
 2050 PROCSQUARE(1,100,500,350)
 2060 PROCSQUARE(2,500,500,350)
 2070 PRINTTAB(50,5) "SCORE"
 2080 PRINTTAB(50,14)"HIGH"
 2090 PRINTTAB(51,15)"SCORE"
 2100 ENDPROC
 2110 :
 2120 :
 3000 DEFPROCGAME
 3010 PRINTTAB(0,47);STRING$(19," ");
 3020 PRINTTAB(52,7);"0  "
 3030 PRINTTAB(52,17);high%
 3040 C%=0
 3050 C%=C%+1
 3060 A%(C%)=RND(4)
 3070 FOR G=1 TO C%
 3080   PROCSOUNDS(A%(G))
 3090 NEXT G
 3100 REM *FX15,1
 3110 player%=0
 3120 REPEAT player%=player%+1
 3130   REPEAT
 3140     A=GET
 3150   UNTIL A>48 AND A<53
 3160   PROCSOUNDS(A-48)
 3170 UNTIL player%=C% OR A-48<>A%(player%)
 3180 IF A-48<>A%(player%) THEN PROCERROR:ENDPROC
 3190 PRINTTAB(52,7);C%;" "
 3200 IF C%>high% THEN COLOUR9:PRINTTAB(52,17);C%:high%=C%:COLOUR7
 3210 PROCDLY(10)
 3220 GOTO3050
 3230 :
 3240 :
 4000 DEFPROCSOUNDS(F%)
 4010 COLOUR F%,0,0,0
 4020 SOUND1,-10,B%(F%),6
 4030 PROCDLY(3)
 4040 PROCRESETCOLS
 4050 PROCDLY(5)
 4060 ENDPROC
 4070 :
 4080 :
 5000 DEFPROCERROR
 5010 SOUND1,-15,0,30
 5020 SOUND2,-15,0,30
 5030 SOUND3,-15,0,30
 5040 PRINTTAB(0,47);"ANOTHER GAME (Y/N)?";
 5050 REPEAT
 5055   REM *FX21,0
 5060   again$=GET$
 5070 UNTIL again$="n" OR again$="y" OR again$="N" OR again$="Y"
 5075 IF again$="n" OR again$="N" THEN CLS:END
 5080 ENDPROC
 5090 :
 5100 :
 6000 DEFPROCDLY(dy)
 6010 FOR wait%=1 TO dy*700
 6020 NEXT wait%
 6030 ENDPROC
 6040 :
 6050 :
 7000 DEFPROCCOFF
 7010 VDU23;11,0;0;0;0
 7020 ENDPROC
 7030 :
 7040 :
 7100 DEFPROCRESETCOLS
 7110 COLOUR 1,255,0,0
 7120 COLOUR 2,255,255,0
 7130 COLOUR 3,0,255,0
 7140 COLOUR 4,0,0,255
 7190 ENDPROC
 8000 DEFPROCSQUARE(col%,xstart%,ystart%,side%)
 8010 GCOL 0,col%
 8020 PLOT 4,xstart%,ystart%
 8025 PLOT 4,xstart%,ystart%+side%
 8030 PLOT 80,xstart%+side%,ystart%
 8040 PLOT 80,xstart%+side%,ystart%+side%
 8050 REM PLOT 81,side%,0
 8060 ENDPROC
 8070 :
 8080 :
 9000 REM *FX4
 9010 REM IFERR=17THEN140
 9020 REM         MODE1
 9030 REPORT
 9040 PRINT" at line ";ERL
