   10 REM ********************************************
   20 REM SIMON SAYS FOR AGON LIGHT
   30 REM BY PNGWEN (ROBERT LOWE)
   40 REM CREATED 22 APRIL 2023
   50 REM FOR THE OLIMEX WEEKEND PROGRAMMING CHALLENGE
   60 REM    ISSUE #1
   70 REM WRITTEN IN BBC BASIC FOR MOS/VDP 1.03
   80 REM ********************************************
   90 MODE 2
  100 VDU 23,1,0 : REM HIDE THE TEXT CURSOR
  110 PROC_FRAME_DELAY(90)
  120 CLS:CLG
  130 REM SET UP THE LIGHTS AND INITIAL POSITION
  140 GX=330:GY=507:GOFF=2:GON=10:GP=33
  150 YX=GX+210:YY=GY-210:YOFF=3:YON=11:YP=69
  160 BX=YX+210:BY=GY:BOFF=16:BON=12:BP=81
  170 RX=YX:RY=GY+210:ROFF=25:RON=9:RP=53
  180 D=3
  190 MAX=100 : REM THIS IS VERY OPTIMISTIC!
  200 DIM SEQ(MAX)
  210 SEQ(0)=RND(4)
  220 REM PLAY A WELCOMING OPENING
  230 PROC_PLAY(1):PROC_PLAY(2):PROC_PLAY(3):PROC_PLAY(4)
  240 PROC_MSG(20,"WELCOME TO SIMON!")
  250 PROC_MSG(21,"PRESS AN ARROW KEY TO START")
  260 PROC_MSG(22,"ANY OTHER KEY TO EXIT")
  270 K=INKEY(0) : IF K=-1 THEN 270
  280 IF K<>11 AND K<>10 AND K<>8 AND K<>21 THEN GOTO 620
  290 PROC_MSG(20,"SIMON SAYS"):PROC_MSG(21,"FOLLOW ME WITH THE ARROW KEYS"):PROC_CLL(22)
  300 PROC_FRAME_DELAY(120)
  310 D=10:SCORE=0
  320 PROC_CLL(0):PRINT TAB(1,0);"SCORE: ";SCORE
  330 REM PLAY SEQ
  340 FOR I=0 TO SCORE
  350   P=SEQ(I)
  360   PROC_PLAY(P)
  370 NEXT I
  380 I=0
  390 REM GET PLAYER MOVE
  400 K=INKEY(200)
  410 P=0
  420 IF K=8 THEN P=1
  430 IF K=10 THEN P=2
  440 IF K=21 THEN P=3
  450 IF K=11 THEN P=4
  460 REM LOOK FOR A LOSING CONDITION
  470 IF P=SEQ(I) THEN PROC_PLAY(P) ELSE GOTO 590
  480 I=I+1
  490 IF I<=SCORE THEN GOTO 400
  500 SCORE=SCORE+1:PRINT TAB(8,0);SCORE
  510 IF SCORE>MAX THEN PROC_MSG(20,"WHOA! YOU BEAT ME!"):GOTO 250
  520 REM EXTEND THE SEQUENCE AND MAYBE SPEED UP
  530 SEQ(SCORE)=RND(4)
  540 IF SCORE>=7 THEN D=7
  550 IF SCORE>=14 THEN D=5
  560 PROC_FRAME_DELAY(D*6)
  570 GOTO 340
  580 REM THE PLAYER HAS LOST!
  590 PROC_BUZZ(SEQ(I))
  600 PROC_MSG(20,"GAME OVER")
  610 GOTO 250
  620 MODE 1
  630 PRINT "THANKS FOR PLAYING!"
  640 END
  650 REM *****
  660 REM DRAW A LIGHT WITH GIVEN POSITION AND COLOR
  670 REM *****
  680 DEF PROC_DRAW_LIGHT(C,X,Y)
  690 X2=X+200:Y2=Y+200
  700 GCOL 0,C
  710 MOVE X,Y
  720 MOVE X2,Y
  730 PLOT 80,X2,Y2
  740 MOVE X,Y2
  750 PLOT 80,X,Y
  760 ENDPROC
  770 REM *****
  780 REM FLASH A LIGHT
  790 REM *****
  800 DEF PROC_FLASH(CON,COFF,X,Y,P)
  810 PROC_DRAW_LIGHT(CON,X,Y)
  820 SOUND 0,-15,P,D
  830 PROC_FRAME_DELAY(D*3)
  840 PROC_DRAW_LIGHT(COFF,X,Y)
  850 ENDPROC
  860 REM *****
  870 REM PLAY THE SELECTED LIGHT 1=G 2=Y 3=B 4=R
  880 REM *****
  890 DEF PROC_PLAY(X)
  900 IF X=1 THEN PROC_FLASH(GON,GOFF,GX,GY,GP)
  910 IF X=2 THEN PROC_FLASH(YON,YOFF,YX,YY,YP)
  920 IF X=3 THEN PROC_FLASH(BON,BOFF,BX,BY,BP)
  930 IF X=4 THEN PROC_FLASH(RON,ROFF,RX,RY,RP)
  940 PROC_FRAME_DELAY(D)
  950 ENDPROC
  960 REM *****
  970 REM DELAY FOR THE SPECIFIED NUM OF FRAMES
  980 REM *****
  990 DEF PROC_FRAME_DELAY(NUM)
 1000 FOR FCOUNT=1 TO NUM
 1010   *FX 19
 1020 NEXT FCOUNT
 1030 ENDPROC
 1040 REM *****
 1050 REM BUZZ THE GIVEN LIGHT
 1060 REM *****
 1070 DEF PROC_BUZZ(X)
 1080 DT=D
 1090 D=40
 1100 IF X=1 THEN PROC_FLASH(GON,GOFF,GX,GY,0)
 1110 IF X=2 THEN PROC_FLASH(YON,YOFF,YX,YY,0)
 1120 IF X=3 THEN PROC_FLASH(BON,BOFF,BX,BY,0)
 1130 IF X=4 THEN PROC_FLASH(RON,ROFF,RX,RY,0)
 1140 D=DT
 1150 ENDPROC
 1160 REM *****
 1170 REM CLEAR A LINE
 1180 REM *****
 1190 DEF PROC_CLL(L)
 1200 PRINT TAB(0,L);TAB(40)
 1210 ENDPROC
 1220 REM *****
 1230 REM DISPLAY A CENTERED MESSAGE
 1240 REM *****
 1250 DEF PROC_MSG(L,M$)
 1260 PROC_CLL(L)
 1270 PRINT TAB(20-INT(LEN(M$)/2),L);M$
 1280 ENDPROC
