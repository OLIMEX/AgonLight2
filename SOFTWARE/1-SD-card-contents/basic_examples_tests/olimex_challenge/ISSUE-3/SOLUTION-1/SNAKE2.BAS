   10 VDU 23,1,0
   20 DIM X(100),Y(100)
   30 CLS
   40 L=1:FX=0:FY=0:XV=1:YV=0
   50 SC=0:SD=100
   60 PROC_ScoreBoard
   70 GO=0
   80 X(0)=20:Y(0)=20
   90 PROC_PlaceFood
  100 PROC_Frame
  110 PROC_PrintSnake
  120 PROC_CheckKey
  130 PROC_MoveSnake
  140 PROC_CheckCollision
  150 PROC_CheckFood
  160 PROC_ScoreBoard
  170 IF GO=1 THEN GOTO 30
  180 GOTO 110
  190 END
  200 DEF PROC_PrintSnake
  210 COLOUR 10
  220 FOR I=0 TO L-1
  230   PRINT TAB(X(I),Y(I));"#"
  240 NEXT I
  250 ENDPROC
  260 DEF PROC_PlaceFood
  270 FX=RND(40):FY=RND(40)
  280 COLOUR 7
  290 PRINT TAB(FX,FY);"@"
  300 ENDPROC
  310 DEF PROC_CheckKey
  320 N=INKEY(SD)
  330 IF N=85 THEN YV=-1:XV=0
  340 IF N=74 THEN YV=1:XV=0
  350 IF N=72 THEN XV=-1:YV=0
  360 IF N=75 THEN XV=1:YV=0
  370 ENDPROC
  380 DEF PROC_CheckFood
  390 IF X(0)=FX AND Y(0)=FY THEN PROC_GrowSnake:PROC_PlaceFood
  400 ENDPROC
  410 DEF PROC_MoveSnake
  420 SOUND 0,-15,5,1
  430 A=X(L-1):B=Y(L-1)
  440 FOR I=L TO 1 STEP -1
  450   X(I)=X(I-1):Y(I)=Y(I-1):NEXT I
  460 IF X(0)<0 THEN X(0)=40
  470 IF X(0)>40 THEN X(0)=0
  480 IF Y(0)<0 THEN Y(0)=40
  490 IF Y(0)>40 THEN Y(0)=0
  500 X(0)=X(0)+XV:Y(0)=Y(0)+YV
  510 PRINT TAB(A,B);" "
  520 ENDPROC
  530 DEF PROC_GrowSnake
  540 SOUND 1,-15,50,3
  550 SD=SD-1
  560 L=L+1
  570 SC=SC+10
  580 X(L-1)=X(L-2)+1:Y(L-1)=Y(L-2)
  590 ENDPROC
  600 DEF PROC_Frame
  610 COLOUR 3
  620 FOR I=0 TO 40:PRINT TAB(0,I);"+":PRINT TAB(40,I);"+":NEXT I
  630 FOR I=0 TO 40:PRINT TAB(I,0);"+":PRINT TAB(I,40);"+":NEXT I
  640 ENDPROC
  650 DEF PROC_CheckCollision
  660 A$=GET$(X(0),Y(0))
  670 IF A$="#" OR A$="+" THEN GO=1:SOUND 2,-15,120,3:SOUND 1,-15,30,1
  680 ENDPROC
  690 DEF PROC_ScoreBoard
  700 COLOUR 11
  710 PRINT TAB(50,10);"SCORE:";SC
  720 PRINT TAB(50,12);"SPEED:";101-SD
  730 COLOUR 6
  740 PRINT TAB(50,20);"U - UP"
  750 PRINT TAB(50,22);"J - DOWN"
  760 PRINT TAB(50,24);"H - LEFT"
  770 PRINT TAB(50,26);"K - RIGHT"
  780 COLOUR 9
  790 PRINT TAB(42,35);"USE CAPSLOCK IF SNAKE"
  800 PRINT TAB(45,36);"IS NOT MOVING"
  810 ENDPROC
